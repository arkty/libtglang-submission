#Использовать tool1cd
#Использовать v8unpack
#Использовать fs

Процедура ЗапуститьОбработку()
	
	// взято из библиотеки gitsync
	КаталогВыгрузки = ОбъединитьПути(ТекущийСценарий().Каталог, "src");
	
	// ищем *.bin, если есть - обрабатываем
	МассивФайловОбычныхФорм = НайтиФайлы(КаталогВыгрузки, "*.bin", Истина);
	Для каждого Файл из МассивФайловОбычныхФорм Цикл
		Если НРег(Файл.Имя) = "form.bin" Тогда
			ТекущийКаталог = ОбъединитьПути(Файл.Путь, Файл.ИмяБезРасширения);
			ФС.ОбеспечитьПустойКаталог(ТекущийКаталог);
			РаспаковатьКонтейнерМетаданных(Файл.ПолноеИмя, ТекущийКаталог);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура РаспаковатьКонтейнерМетаданных(Знач ФайлРаспаковки, Знач КаталогРаспаковки, Знач Переименования = "", Знач КорневойКаталог = "")
	
	dllРаспаковать(ФайлРаспаковки, КаталогРаспаковки);
	ВыполнитьСборкуМусора(); // см. камент к процедуре dllРаспаковать
	
	Для Каждого ФайлМодуля Из НайтиФайлы(КаталогРаспаковки, "module", Истина) Цикл
		
		СтароеИмяФайла = ФайлМодуля.ПолноеИмя;
		НовоеИмяФайла = ОбъединитьПути(ФайлМодуля.Путь, "Module.bsl");
		ПереместитьФайл(СтароеИмяФайла, НовоеИмяФайла);
		Если НЕ ПустаяСтрока(Переименования) Тогда
			Если ПустаяСтрока(КорневойКаталог) Тогда
				ВызватьИсключение 
				"РаспаковатьКонтейнерМетаданных при заполненном Переименования ожидали и не пустое КорневойКаталог";
			КонецЕсли;
			ДобавитьПереименование(Переименования,
			СтрЗаменить(СтароеИмяФайла, КорневойКаталог, ""),
			СтрЗаменить(НовоеИмяФайла, КорневойКаталог, ""));
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция ДобавитьПереименование(Знач Переименования, Знач Источник, Знач Приемник)
	
	СтрокаПереименования = Переименования.Добавить();
	СтрокаПереименования.Источник = Источник;
	СтрокаПереименования.Приемник = Приемник;
	
	Возврат СтрокаПереименования;
	
КонецФункции

Процедура dllРаспаковать(Знач ФайлРаспаковки, Знач КаталогРаспаковки)
	
	Распаковщик = Новый ЧтениеФайла8(ФайлРаспаковки);
	Распаковщик.ИзвлечьВсе(КаталогРаспаковки, Истина);
	ОсвободитьОбъект(Распаковщик); // почему-то этого недостаточно. Вопрос к реализации компоненты.
	Распаковщик = Неопределено;
	
КонецПроцедуры

ЗапуститьОбработку();